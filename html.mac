	.title	html
	.ident	/X00.50/
	.mcall	qiow$s,exit$s,rref$s,craw$s,sdat$s
	.mcall	astx$s,spnd$s,srra$s,umap$s,dtrg$s
	.mcall	rdbbk$,wdbbk$
	.mcall	ordie,die
	.mcall	save02,save05

;; Initialize a HTML context.
;; Client buffer is at CLI.
;; Place context at LOC.
	.macro	ctxini	loc,cli,url
	mov	url,-(sp)
	mov	cli,-(sp)
	mov	loc,-(sp)
	call	ht$ini
	add	#6,sp
	.endm

;; Begin using existing HTML context.
;; Context is at LOC.
;; Save default context addr at CTX.
	.macro	ctxuse	loc,ctx=$$$ctx
	mov	#ctx,-(sp)
	mov	loc,-(sp)
	call	ht$use
	add	#4,sp
	.endm

;; Respond with a string.
;; STR points to ASCIZ string
	.macro	respnd	str,ctx=$$$ctx
	mov	ctx,-(sp)
	mov	str,-(sp)
	call	ht$res
	add	#4,sp
	.endm

;; Respond with a tag
;; NAME is a literal HTML tag.
	.macro	tag	name,ctx=$$$ctx
	.save
	.psect	htmdat,d,ro
$$$tag	=	.
	.asciz	!<name>!
	.restore
	respnd	#$$$tag,ctx
	.endm

;; Get the response string and its length
;; from context.
;; Returns addr of string in R0
;; and length in R1
	.macro	ctx2st	ctx=$$$ctx
	mov	ctx,r0
	call	ht$2st
	.endm

;; Response is ready. Inform the server.
	.macro	ctxrdy	ctx=$$$ctx
	mov	ctx,-(sp)
	call	ht$rdy
	add	#2,sp
	.endm

;; Wrapper around $EDMSG.
;; Format is at FMT.
;; Parameter list is at PARAM.
;; Outputs to the context.
	.macro	htmsg	fmt,param,ctx=$$$ctx
	mov	ctx,-(sp)
	mov	fmt,-(sp)
	mov	param,-(sp)
	call	ht$msg
	add	#6,sp
	.endm

;; Prepare to receive requests from the server.
;; Should be the last statement in the initialization.
;; AST routine is at AST
	.macro	htmast	ast
	mov	ast,-(sp)
	jmp	ht$ast
	.endm

;; Prepare to process a request.
;; Should be the first statement in the AST routine.
;; Actual processing should follow.
	.macro	htmbgn	?out
	mov	#out,-(sp)
	jmp	ht$bgn
out:
	.endm

;; Get query argument.
;; ARG points to argument name.
;; Place value to VALUE.
;; Clear Carry on success.
	.macro	getarg	arg,value,ctx=$$$ctx,?ok,?out
	mov	ctx,-(sp)
	mov	arg,-(sp)
	mov	value,-(sp)
	call	ht$arg
	bcc	ok
	add	#6,sp
	sec
	br	out
ok:	add	#6,sp
	clc
out:
	.endm

;; Offsets in context definition.
	.asect
.	=	0
ht.cli::.blkw	1		; Client descriptor in the server.
ht.url::.blkb	256.		; Requested URL.
ht.usz	== .	- ht.url	; Maximum URL size.
ht.siz::.blkw	1		; Response length.	
ht.str::			; Response string.
ht.max	==	20000		; Single APR.	

	.psect	htmctx,d,rw
$$$ctx::.word	0		; Current context address.	
wdb:	wdbbk$	7,200,,,,,rcvbuf; Window description for context.	
rdb:	rdbbk$			; Region description for context.	
rcvbuf:	.blkw	10.		; Receive buffer.

	.psect	htmcod,i,ro
ht$res::save02			; Save R0..R2.	
	mov	sarg(sp),r1	; String => R1.	
	mov	sarg+2(sp),r0	; Context => R0.	
	add	ht.siz(r0),r0	; R0 now points to the first	
	add	#ht.str,r0	; unused byte in the context.	
1$:	movb	(r1)+,r2	; Copy string to the context.	
	beq	2$
	movb	r2,(r0)+
	br	1$
2$:	mov	sarg+2(sp),r1	; Context => R1.
	sub	#ht.str,r0
	sub	r1,r0		; Response length => R0.	
	mov	r0,ht.siz(r1)	; Save it.	
	return

ht$2st::mov	ht.siz(r0),r1	; Response size => R1.	
	add	#ht.str,r0	; Response => R0.	
	return

ht$use::save02
	mov	sarg(sp),@sarg+2(sp); Save context address to	
	return			; the specified location.	

ht$ini::save05
	mov	sarg(sp),r0	; Context => R0.	
	clr	ht.siz(r0)	; Response size is 0.	
	mov	sarg+2(sp),ht.cli(r0)	; Save client descriptor.	
	add	#ht.url,r0	; Request URL buffer => R0.	
	mov	#ht.usz-1,r1	; Buffer size => R1.	
	mov	sarg+4(sp),r2	; URL address => R2.	
1$:	movb	(r2)+,r3	; Next char => R3.
	cmpb	#' ,r3		; Is it a terminator?
	bge	2$		; Yes. Finish.
	movb	r3,(r0)+	; No. Save it.
	sob	r1,1$		; Repeat.
2$:	clrb	@r0		; Terminate URL string.
	return

ht$rdy::save02
	mov	sarg(sp),r0	; Context => R0.	
	add	#ht.cli,r0	; Client descriptor => R0.	
	sdat$s	#rcvbuf,r0	; Inform server that client at R0 is ready.	
	ordie	<HT$RDY: cannot send data>
	return

ht$msg::save02
	mov	sarg(sp),r2	; Parameter block => R2.	
	mov	sarg+2(sp),r1	; Format => R1.	
	mov	sarg+4(sp),r0	; Context => R0.	
	add	ht.siz(r0),r0
	add	#ht.str,r0	; First unused byte in context => R0.	
	call	$edmsg		; Construct response.	
	mov	sarg+4(sp),r1	; Context => R1	
	sub	#ht.str,r0
	sub	r1,r0		; New response length => R0.	
	mov	r0,ht.siz(r1)	; Save it.	
	return

ht$bgn::rref$s	#wdb		; Receive a reference to a region.	
	bcc	1$		; Success?	
	cmp	#ie.its,$dsw	; No. Emtpy queue?	
	bne	rcverr		;  No. Bad.	
	tst	(sp)+		;  Yes. Done	
	astx$s
1$:	mov	wdb+w.nrid,rdb+r.gid	; Save RegionID.	
	ctxuse	wdb+w.nbas	; Output to the received region.	
	mov	@sp,r0		; Actual handler addr => R0.	
	call	@r0		; Build a response.	
	ctxrdy			; Done.	
	dtrg$s	#rdb		; Detach the region.	
	br	ht$bgn		; Is there any work left?	
rcverr:	die	<HT$BGN: Cannot receive by reference>

ht$ast::craw$s	#wdb		; Create an address window	
	ordie	<HT$AST: Cannot create window>
	bis	#ws.map,wdb+w.nsts	; Map on RREF.	
	srra$s	(sp)+		; Handler was in stack.	
	ordie	<HT$AST: Cannot set AST>
1$:	spnd$s			; Wait for requests.	
	br	1$

ht$arg::save05
	mov	sarg+2(sp),r3	; Argument name => R3.	
	mov	sarg+4(sp),r1	; Context => R1.
	add	#ht.url,r1	; R1 points at URL.
	movb	#'?,r2		; Look for query arguments.
1$:	call	fndchr		; Delimiter found?
	bcs	10$		; No.	
	mov	r3,r0		; Look for argument name
	movb	#'=,r2		; terminated by =.	
	call	tokcmp		; Argument found?
	bcc	7$		; Yes.
	movb	#'&,r2		; Look for the next argument.	
	br	1$
7$:	mov	sarg(sp),r0	; Buffer for argument value => R0.
8$:	movb	(r1)+,r2	; Next char => R2.
	beq	9$		; End of string?
	cmpb	#'&,r2		; Beginning of the next argument?
	beq	9$	
	movb	r2,(r0)+	; Save character.
	br	8$		; Repeat.	
9$:	clrb	@r0		; Terminate string.	
	mov	sarg(sp),r0	; Buffer => R0.
	call	urldec		; URL decode result.
	clc			; Success.
	return
	
10$:	sec			; No such argument.	
	return

	.sbttl	String utilities

;+
; Check if the token is present in string.
; R0 points to ASCIZ token.
; R1 points to string to check.
; R2 points to string token delimiter.
; Carry bit is cleared if token is found.
; R1 is updated to point after token and delimiter.
;-
tokcmp::save02
1$:	tstb	@r0
	beq	3$
	cmpb	(r0)+,(r1)+
	beq	1$
2$:	sec
	return
3$:	cmpb	r2,@r1
	bne	2$
	inc	r1
	mov	r1,s1(sp)
	clc
	return

;+
; Find character in ASCIZ string.
; R1 points to string.
; R2 contains the character.
; Carry bit is cleared if character is found.
; R1 is updated to point after the character.
;-
fndchr::save02
1$:	tstb	@r1
	beq	2$
	cmpb	(r1)+,r2
	bne	1$
	mov	r1,s1(sp)
	clc
	return
2$:	sec
	return

;+
; URL decode in place.
; R0 points at strings.
;-
urldec::save05
	mov	r0,r1		; Input ptr => R0, output ptr => R1.
1$:	movb	(r0)+,r2	; Read next char.
	beq	10$		; End of string?
	cmpb	#'%,r2		; URL-encoded character?
	beq	2$		; Yes, decode it.
	movb	r2,(r1)+	; No, leave it alone.	
	br	1$		; Repeat.	
2$:	mov	#2,r3		; Digit counter => R3.
	clr	r4		; Decoded char => R4.
3$:	ash	#4,r4		; Next result digit.
	movb	(r0)+,r2	; Next digit => R2.
	cmpb	r2,#'0
	blt	9$
	cmpb	r2,#'9
	bgt	4$
	sub	#'0,r2
	br	8$
4$:	cmpb	r2,#'A
	blt	9$
	cmpb	r2,#'F
	bgt	5$
	sub	#'A,r2
	br	7$
5$:	cmpb	r2,#'a
	blt	9$
	cmpb	r2,#'f
	bgt	9$
	sub	#'a,r2
7$:	add	#10.,r2
8$:	add	r2,r4
	sob	r3,3$
	movb	r4,(r1)+
9$:	br	1$
10$:	clrb	@r1		; Terminate string.
	return

	.end
