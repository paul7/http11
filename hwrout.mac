	.title	hwrout
	.ident	/X00.20/
	.mcall	qiow$s,exit$s,rref$s,craw$s,setf$s
	.mcall	astx$s,spnd$s,srra$s,umap$s,dtrg$s
	.mcall	wdbbk$,rdbbk$
	.mcall	ordie,die
	.mcall	tag,respnd,ctx2st,ctxuse,ctxrdy,htmsg

	.psect	da,d,ro
hw::	.asciz	/Hello World from RSX-11M-PLUS!/
refrnc::.asciz	/Link/
info::	.asciz	/Visitor #%D. RequestID = %P/

	.psect	db,d,rw
wdb:	wdbbk$	7,200
rdb:	rdbbk$

	.psect	ctr,d,rw
infdat::
ctr::	.word	0
reqid::	.word	0

	.psect	main,i,ro
start::	craw$s	#wdb
	ordie	<Cannot create window>
	bis	#ws.map,wdb+w.nsts
	srra$s	#rqst
	ordie	<Cannot set AST>
	spnd$s
	ordie	<Cannot suspend>
	exit$s

rqst::	rref$s	#wdb
	bcc	1$
	cmp	#ie.its,$dsw
	bne	rcverr
	astx$s
1$:	mov	wdb+w.nrid,rdb+r.gid
	ctxuse	wdb+w.nbas
	tag	<p>
	respnd	#hw
	tag	</p>
	tag	<p>
	tag	<a href=/BYE>
	respnd	#refrnc
	tag	</a>
	tag	</p>
	mov	$$$ctx,r0
	mov	ht.cli(r0),reqid
	htmsg	#info,#infdat
	inc	ctr
	ctxrdy
	dtrg$s	#rdb
	jmp	rqst
rcverr:	die	<Cannot receive by reference>

	.end	start
