	.title	http11
	.ident	/X00.50/
	.mcall	crrg$s,craw$s,sref$s,qiow$s,qio$s
	.mcall	astx$s,dsar$s,enar$s,srda$s,rcvd$s
	.mcall	spnd$s,alun$s,map$s,mrkt$s,gtim$s
	.mcall	dtrg$s,setf$s,decl$s
	.mcall	rdbbk$,wdbbk$
	.mcall	ordie,die,dielun
	.mcall	ctxini,ctxuse,ctx2st
	.mcall	save02,save05
	.mcall	useoff

	.nlist	me

port	=	1337.

tilun	=	1
tclun	=	2
tcefn	=	1
tiefn	=	2
conefn	=	2

clinum	=	20
quesiz	=	20
blun	=	4
befn	=	65.

sopt	=	21
tmout	=	30.

maxage	=	5
wtchps	=	5

cl.free	=	0
cl.read =	1
cl.work	=	2
cl.hdr	=	40000
cl.err	=	100000

	useoff	context
	useoff	client

	.sbttl	Error Handling Macros

	.macro	deferr	code,desc
	.save
	.psect	errors,d,ro
hdr'code:
	.ascii	|HTTP/1.1 code desc|<15><12>
	.ascii	|Content-Length: |
hdl'code=	. - hdr'code
msg'code:
	.ascii	|code: desc|
msl'code=	. - msg'code

	.psect	errcod,i,ro
rsp'code::
	mov	r1,r4
	add	#c.iost,r4
	mov	r1,r5
	add	#c.tmp,r5
	qiow$s	#io.can,c.lun(r1),#conefn
	orclos
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<#hdr'code,#hdl'code>
	orclos
	mov	r1,r3
	mov	r5,r0
	mov	#msl'code,r1
	clr	r2
	call	$cbdmg
	sub	r5,r0
	mov	r3,r1
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<r5,r0>
	orclos
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<#hdrend,#hdlend>
	orclos
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspdon,<#msg'code,#msl'code>
	orclos
	astx$s
	.restore

	.macro	or'code	cli,iosb,?ok,?bad
	.if nb iosb
	bcs	bad
	tstb	iosb
	bpl	ok
	.iff
	bcc	ok
	.endc
bad:
	.if nb cli
	mov	cli,r1
	.endc
	jmp	rsp'code
ok:
	.endm	or'code
	.endm	deferr	

	.macro	orjmp	label,iosb,?ok,?bad
	.if	nb	iosb
	bcs	bad
	tstb	iosb
	bpl	ok
	.iff
	bcc	ok
	.endc
bad:	jmp	label
ok:
	.endm

	.macro	orclos	cli,iosb,?ok,?bad
	.if nb iosb
	bcs	bad
	tstb	iosb
	bpl	ok
	.iff
	bcc	ok
	.endc
bad:
	.if nb cli
	mov	cli,r1
	.endc
	jmp	close
ok:
	.endm

	deferr	400,<Bad Request>
	deferr	404,<Not Found>
	deferr	408,<Request Timeout>
	deferr	500,<Internal Server Error>
	deferr	503,<Service Unavailable>

	.sbttl	Data Definition

	.psect	dir,d,rw
regsiz	=	200
rdb::	rdbbk$	regsiz,,,rs.att!rs.red!rs.wrt,170000
rdbsiz	=	. - rdb
wdb::	wdbbk$	7,regsiz,0,0,,ws.wrt,reqid

	.psect	regdef,d,rw
clirdb::.blkb	rdbsiz * clinum

	.psect	clidef,d,rw
reqid::	.word	0
topcli::.word	0
client::.blkb	c.size * clinum

	.psect	tmp,d,rw
rspbuf:	.blkw	15.
reqbuf:	.blkw	100
reqbl	=	. - reqbuf
iosb:	.blkw	2
date:	.blkw	8.
timer:	.blkw	1
ep:	.blkw	3

	.psect	handlr,d,rw
hndtbl:	.word	0
	.rad50	/ABOUT/
	.word	^rABO
	.rad50	/ABOUT/
	.word	^rDIC
	.rad50	/DICERT/
	.word	^rQAR
	.rad50	/QARG/
	.word	^rBIG
	.rad50	/BIG   /
	.word	^rHEL
	.rad50	/HWROUT/
	.word	^rBYE
hndend:	.rad50	/BYROUT/

	.psect	const,d,ro
chnend:	.byte	15,12
chnenl	=	. - chnend
lstchn:	.asciz	/0/<15><12><15><12>
lstchl	=	. - lstchn
reqget:	.asciz	|GET|
hdr200:	.ascii	|HTTP/1.1 200 OK|<15><12>
	.ascii	|Content-Type: text/html; charset=utf-8|<15><12>
	.ascii	|Transfer-Encoding: chunked|<15><12>
hdl200	=	. - hdr200
hdrend:	.ascii	|Server: HTTP/11 X00.50|<15><12>
	.ascii	|Connection: close|<15><12>
	.ascii	<15><12>
hdlend	=	. - hdrend

	.sbttl	Initialization

	.psect	code,i,ro
start::	alun$s	#tilun,#"TI
	dielun	#tilun
	call	inicli
	crrg$s	#rdb
	ordie	<Cannot create region>
	craw$s	#wdb
	ordie	<Cannot create window>
	srda$s	#rsprdy
	ordie	<Cannot set receive AST>

	alun$s	#tclun,#"TC
	ordie	<Cannot assign listening LUN>

	gtim$s	#date
	ordie	<Cannot get time information>
	mov	date+g.ticp,r0
	mov	#wtchps,r1
	call	$div
	mov	r0,timer

	dsar$s

	qiow$s	#io.cep,#tclun,#tcefn,,#iosb,,<#port>
	ordie	<Cannot create endpoint>,iosb
	qiow$s	#io.ras,#tclun,#tcefn,,#iosb,,<#conrdy>
	ordie	<Cannot regiser AST>,iosb
	qiow$s	#io.lsn,#tclun,#tcefn,,#iosb,,<,#clinum.>
	ordie	<Cannot listen>,iosb

	mrkt$s	,timer,#1,#watchd
	ordie	<Cannot setup watchdog timer>

	enar$s
1$:	spnd$s
	br	1$

	.sbttl	AST Routines

watchd::tst	(sp)+
	mov	#clinum,r0
	mov	#client,r1
1$:	tst	c.rqid(r1)
	beq	10$
	inc	c.age(r1)
	cmp	#maxage,c.age(r1)
	bhi	10$
	qiow$s	#io.cln,c.lun(r1),#conefn
	call	mapcli
	mov	wdb+w.nbas,r2
	clr	h.rqid(r2)
	cmp	#maxage,c.age(r1)
	beq	10$
	tst	h.drty(r2)
	beq	9$
	call	reicli
9$:	call	relcli
10$:	add	#c.size,r1
	sob	r0,1$
	mrkt$s	,timer,#1,#watchd
	ordie	<Cannot re-setup watchdog timer>
	astx$s

conrdy::tst	(sp)+
	qiow$s	#io.ast,#tclun,#tcefn
	call	getcli
	tst	r1
	bne	1$
	qiow$s	#io.rej,#tclun,#tcefn,,#iosb
	br	2$
1$:	qiow$s	#io.rvb,#tclun,#tcefn,,#iosb,,<#ep,#6>
	orjmp	2$
	qiow$s	#io.cep,c.lun(r1),#conefn,,#iosb,,<#port,#ep>
	orclos	,iosb
	qiow$s	#io.acc,c.lun(r1),#conefn,,#iosb,,<0,#sopt,#tmout>
	orclos	,iosb
	mov	r1,r2
	add	#c.iost,r2
	mov	r1,r3
	add	#c.rqst,r3
	qio$s	#io.rlb,c.lun(r1),#conefn,,r2,#reqrdy,<r3,#c.rsz>
	orclos
2$:	astx$s

reqrdy::mov	(sp)+,r1
	mov	r1,r2
	sub	#c.iost,r1
	orclos	,r2
	beq	10$
	mov	2(r2),r2	; Number of bytes read => R2.
	mov	r1,r3
	add	#c.rqst,r1
	add	r1,r2		;  End of request => R2.
	clrb	@r2
	mov	#reqget,r0
	mov	#' ,r2
	call	tokcmp
	bcs	1$
	cmpb	#'/,(r1)+
	beq	2$
1$:	mov	r3,r1
	jmp	rsp400
2$:	mov	r1,r0
	call	fndhnd
	bcc	3$
	mov	r3,r1
	jmp	rsp404
3$:	mov	r1,r2
	mov	r3,r1
	call	mapcli
	ctxini	wdb+w.nbas,r1,r2
	sref$s	r0,#wdb,#1
	or503
10$:	astx$s

rsprdy::rcvd$s	,#rspbuf
	bcc	1$
	cmp	#ie.its,$dsw
	bne	2$	
	astx$s
2$:	jmp	rcverr	
1$:	mov	rspbuf+4,r1
	call	mapcli
	mov	wdb+w.nbas,r0
	cmp	h.rqid(r0),c.rqid(r1)
	bne	rsprdy
	bit	#cl.hdr,c.stat(r1)
	bne	3$
	bis	#cl.hdr,c.stat(r1)
	mov	r1,r4
	add	#c.iost,r4
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<#hdr200,#hdl200>
	orclos
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<#hdrend,#hdlend>
	orclos	
3$:	ctxuse	r0
	mov	r1,-(sp)
	ctx2st
	mov	r0,-(sp)
	mov	r1,-(sp)
	call	chunk
	add	#4,sp
	mov	(sp)+,r1
	mov	wdb+w.nbas,r0
	tst	h.done(r0)
	beq	10$
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspdon,<#lstchn,#lstchl>
	orclos
10$:	jmp	rsprdy

chunk::	save05
	mov	sarg+4(sp),r0
	add	#c.tmp,r0
	mov	r0,r5
	mov	sarg(sp),r1
	mov	#100020,r2
	call	$cbta
	movb	#15,(r0)+
	movb	#12,(r0)+
	sub	r5,r0
	mov	sarg+4(sp),r1
	mov	r1,r4
	add	#c.iost,r4
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<r5,r0>
	orclos
	mov	sarg(sp),r2
	mov	sarg+2(sp),r3
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspast,<r3,r2>
	orclos
	qio$s	#io.wlb,c.lun(r1),#conefn,,r4,#rspnxt,<#chnend,#chnenl>
	orclos
	return

rspnxt::mov	(sp)+,r1
	mov	r1,r2
	sub	#c.iost,r1
	ordie	,r2
	call	mapcli
	mov	wdb+w.nbas,r2
	clr	h.size(r2)
	setf$s	c.efn(r1)
	decl$s
	astx$s

rspdon::mov	(sp)+,r1
	mov	r1,r2
	sub	#c.iost,r1
	setf$s	c.efn(r1)
	jmp	close
rcverr:	die	<Cannot receive data>

close::	qiow$s	#io.cln,c.lun(r1),#conefn
	call	relcli
	astx$s

rspast::mov	(sp)+,r1
	mov	r1,r2
	sub	#c.iost,r1
	orclos	,r2
	astx$s

	.sbttl	Utilities

;+
; Find handler for URL.
; R1 points at URL.
; Handler table is at HNDTBL.
; Its last entry is at HNDEND.
; Handler task name is returned in R0.
; Carry bit is cleared on success.
;-
fndhnd::save02
	clr	r1
	call	$cat5
	bcc	4$
	cmpb	#' ,r2
	bne	3$
4$:	mov	#hndtbl,r0
1$:	cmp	(r0)+,r1
	beq	2$
	cmp	r0,#hndend
	bge	3$
	cmp	(r0)+,(r0)+
	br	1$
2$:	mov	r0,s0(sp)
	clc
	return
3$:	sec
	return

	.sbttl	Client Descriptor Utilities

;+
; Initialize client descriptors.
; Client descriptor area is at CLIENT.
; CLINUM is a number of descriptors to initialize.
; BLUN is the lowest unused LUN.
; Region definitions are at CLIRDB.
; Descriptors are a linked list, TOPCLI points at its head.
;-
inicli::save05
	mov	#clinum,r1
	mov	#client,r0
	mov	#clirdb,r3
1$:	mov	#clinum,c.lun(r0)
	add	#blun,c.lun(r0)
	alun$s	c.lun(r0),#"TC
	bcs	lunerr
	mov	#clinum,c.efn(r0)
	add	#befn,c.efn(r0)
	clr	c.rqid(r0)
	clr	c.age(r0)
	mov	r3,c.rgid(r0)
	mov	topcli,c.next(r0)
	mov	r0,topcli
	mov	#rdbsiz,r5
	mov	#rdb,r4
2$:	movb	(r4)+,(r3)+
	sob	r5,2$
	add	#c.size,r0
	sob	r1,1$
	mov	#clirdb,r0
	mov	#clinum,r1
3$:	crrg$s	r0
	bcs	regerr
	add	#rdbsiz,r0
	sob	r1,3$
	return
regerr:	die	<Cannot create region>
deterr:	die	<Cannot detach region>
lunerr:	die	<Cannot assign LUN>

;+
; Reinitialize stuck client.
; Release its region and make a new one.
; Client descriptor is in R1.
;-
reicli::save05
	dtrg$s	c.rgid(r1)
	bcs	deterr
	mov	#rdbsiz,r5
	mov	#rdb,r4
	mov	c.rgid(r1),r3
1$:	movb	(r4)+,(r3)+
	sob	r5,1$
	crrg$s	c.rgid(r1)
	bcs	regerr
	return

;+
; Pop a client descriptor from the pool.
; Return the descriptor in R1.
; R1 = 0 if pool is empty.
;-
getcli::mov	topcli,r1
	beq	1$
	add	#1,reqid	; Set Carry on overflow!	
	adc	reqid
	mov	reqid,c.rqid(r1)
	mov	#cl.free,c.stat(r1)
	mov	c.next(r1),topcli
1$:	return

;+
; Push a client descriptor to the pool.
; Descriptor is in R1.
;-
relcli::mov	topcli,c.next(r1)
	mov	r1,topcli
	clr	c.rqid(r1)
	clr	c.age(r1)
	return

;+
; Map address window to the client's region.
; Client descriptor is in R1.
;-
mapcli::save02
	mov	r1,r2
	add	#c.rgid,r2
	mov	@r2,r2
	mov	r.gid(r2),wdb+w.nrid
	map$s	#wdb
	orclos
	return

	.end	start
